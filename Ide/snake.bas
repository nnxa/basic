10 DIM GRID%(80, 50)
20 FOR X% = 1 TO 80: GRID%(X%, 1) = 15: GRID%(X%, 50) = 15: NEXT X%
30 FOR Y% = 1 TO 50: GRID%(1, Y%) = 15: GRID%(80, Y%) = 15: NEXT Y%
40 DIM SNAKEX%(1000): DIM SNAKEY%(1000)
50 I% = 1
60 FOR X% = 36 TO 44
70 SNAKEX%(I%) = X%: SNAKEY%(I%) = 25
80 I% = I% + 1
90 NEXT X%
100 HEAD% = I% - 1
110 TAIL% = 1
120 FOR I% = TAIL% TO HEAD%
130 GRID%(SNAKEX%(I%), SNAKEY%(I%)) = 15
140 NEXT I%
150 FOODX% = INT(RND * 80) + 1: FOODY% = INT(RND * 50) + 1
151 IF GRID%(FOODX%, FOODY%) <> 0 THEN GOTO 150
152 GRID%(FOODX%, FOODY%) = 4
153 GOSUB 1000
160 D% = 2
170 X% = SNAKEX%(HEAD%)
180 Y% = SNAKEY%(HEAD%)
190 IF D% = 1 THEN Y% = Y% - 1
200 IF D% = 2 THEN X% = X% + 1
210 IF D% = 3 THEN Y% = Y% + 1
220 IF D% = 4 THEN X% = X% - 1
230 IF GRID%(X%, Y%) = 15 THEN END
231 IF GRID%(X%, Y%) = 4 THEN GOSUB 1200
240 C% = 15: GOSUB 1100
250 HEAD% = HEAD% + 1
260 IF HEAD% > 1000 THEN HEAD% = 1
265 IF HEAD% = TAIL% THEN END
270 SNAKEX%(HEAD%) = X%: SNAKEY%(HEAD%) = Y%
275 IF GROW% > 0 THEN GROW% = GROW% - 1: GOTO 310
280 X% = SNAKEX%(TAIL%): Y% = SNAKEY%(TAIL%): C% = 0: GOSUB 1100
290 TAIL% = TAIL% + 1
300 IF TAIL% > 1000 THEN TAIL% = 1
310 FOR I% = 1 TO 30000: NEXT I%
320 K% = INKEY%
330 IF K% = 37 AND D% <> 2 THEN D% = 4
340 IF K% = 38 AND D% <> 3 THEN D% = 1
350 IF K% = 39 AND D% <> 4 THEN D% = 2
360 IF K% = 40 AND D% <> 1 THEN D% = 3
370 GOTO 170
1000 CLS
1010 FOR Y% = 1 TO 49 STEP 2
1020 FOR X% = 1 TO 80
1030 COLOR GRID%(X%, Y% + 1), GRID%(X%, Y%)
1040 PRINT "▄";
1050 NEXT X%
1060 PRINT
1070 NEXT Y%
1080 COLOR 7, 0
1090 RETURN
1100 GRID%(X%, Y%) = C%
1110 Y2% = ((Y% + 1) / 2)
1120 LOCATE Y2%, X%
1130 COLOR GRID%(X%, Y2% * 2), GRID%(X%, Y2% * 2 - 1)
1140 PRINT "▄";
1150 LOCATE 26, 1: RETURN
1200 GROW% = GROW% + 5
1210 FOODX% = INT(RND * 80) + 1: FOODY% = INT(RND * 50) + 1
1220 IF GRID%(FOODX%, FOODY%) <> 0 THEN GOTO 1210
1230 TX% = X%: TY% = Y%
1240 X% = FOODX%: Y% = FOODY%: C% = 4: GOSUB 1100
1250 X% = TX%: Y% = TY%
1260 RETURN
